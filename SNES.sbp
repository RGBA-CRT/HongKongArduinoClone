'スーファミシステム系

Const SFC_SPEC_HEADER_SIZE=32
Const SFC_SPEC_HEADER_ADDR=&HFFC0

Const HiROM_START_ADR=&HC00000

Const SRAM_ADDRESS_LoROM_NORMAL = &H700000'&H380000
Const SRAM_ADDRESS_LoROM_MAD 	= &H384000
Const SRAM_ADDRESS_HiROM_NORMAL = &H1C0000
Const SRAM_ADDRESS_HiROM_MAD 	= &H206000'&HA06000
Const SRAM_ADDRESS_SA1 = &H400000

Const SFC_ROM_TYPE_NUM = 6
Enum SFC_ROM_TYPE
	SFC_ROM_LoROM = 0
	SFC_ROM_HiROM = 1
	SFC_ROM_SDD1= 2
	SFC_ROM_SA1	= 3
	SFC_ROM_ExHiROM = 5
	SFC_ROM_SPC7110 = &H0A
End Enum
	
Type SFC_ROM_HEADER
	Title[21] AS Byte
	MapMode AS Byte
	ROMType As Byte
	CartType AS Byte
	ROMSize AS DWord
	SRAMSize AS DWord
	SRAMAddress As DWord
	Country AS Byte
	Version AS Byte
	CheckSum1 AS Word
	CheckSum2 AS Word
	Mad AS Byte
EndType

'ROMの吸出し
Sub DumpFullROM(info AS *SFC_ROM_HEADER,Path AS BytePtr)
	Dim MAX_CONTINUE=DEFAULT_CONTINUE As DWord
	Dim DUMP_BUFFER_SIZE=DEFAULT_DUMP_SIZE AS DWord
	Dim DumpAddress As DWord, offset AS DWord, ret AS DWord, CheckSum=0 AS DWord,
	Dim sdd1Bank=0 AS Long

	Dim buf AS BytePtr,nowSize As DWord,Size AS DWord,bRomType AS Byte
	Dim hout AS HANDLE,C AS Long	'コンテニューカウンタ
	Dim lt AS DWord,time AS DWord

	'アドレス系準備
	if info->ROMType=SFC_ROM_HiROM or info->ROMType=SFC_ROM_SA1  or info->ROMType=SFC_ROM_SDD1  Then
		bRomType=FALSE
		DumpAddress=HiROM_START_ADR
		offset=HiROM_START_ADR
	Else
		bRomType=TRUE
		DumpAddress=0
		offset=0
	Endif
	Size=info->ROMSize
	SendControl(hCOM,DEFAULT_CTRLBUS)

	'設定ファイル読み込み
	MAX_CONTINUE=GetPrivateProfileInt(ProgramName,"MaxContinue",DEFAULT_CONTINUE,INIFILE_PATH)
	DUMP_BUFFER_SIZE=GetPrivateProfileInt(ProgramName,"DumpBufferSize",DEFAULT_DUMP_SIZE,INIFILE_PATH)
	if DUMP_BUFFER_SIZE <= 0 Then DUMP_BUFFER_SIZE=DEFAULT_DUMP_SIZE

	'UI系
	SendMessage( MWnd(hProg), PBM_SETPOS, 0, 0 )
	SendMessage(MWnd(hProg), PBM_SETRANGE,0,MAKELONG(0,(Size/DUMP_BUFFER_SIZE) As Word))
	SendMessage(MWnd(hProg), PBM_SETSTEP,1,0)
	SetDlgEnable(STATUS_DUMPING)

	'SDD1のセットアップ
	if info->ROMType=SFC_ROM_SDD1 Then
		sdd1Bank=0
		SDD1_SetBanks(hCOM,sdd1Bank)
		SDD1_PrintBanks(hCOM)

		DUMP_BUFFER_SIZE=DEFAULT_DUMP_SIZE	'0x400000で必ず止まる必要が有るため
	End If

	'ダンプバッファの作成	
	buf=calloc(DUMP_BUFFER_SIZE+12)
	hout=OpenFile(Path,GENERIC_WRITE)

	DBM("(Pless Esc to Cancel)")
	DBMN("Dumping")

	lt=GetTickCount()

	Do
		'ESCキーでキャンセル
		PumpMessage()
		if GetKeyState(VK_ESCAPE) And &H80 Then DBM(ex"\r\nCancelled by user."):Goto *EXIT_DUMP

		'吸出し＆データチェック
		ret=ReadROM(hCOM,buf, 0, DumpAddress, DUMP_BUFFER_SIZE,bRomType)
		if ret=0 Then DBM("ERROR!"):Goto *EXIT_DUMP
		if ret<>DUMP_BUFFER_SIZE Then 
			DBM(ex"\r\n["+Str$(C)+ex"]Warning!")
			C++
			if C>MAX_CONTINUE Then DBM("Over retry count.")
			Continue
		endif

		'出力＆チェックサム計算
		OutFile(hout,buf,DUMP_BUFFER_SIZE)
		CheckSum+=CalcChkSum(buf,DUMP_BUFFER_SIZE)

		'次のアドレスを計算
		DumpAddress+=DUMP_BUFFER_SIZE
		if DumpAddress-offset=&H400000 Then
			'4MB超えする時
			if info->ROMType=SFC_ROM_SDD1 Then
				if sdd1Bank<4 Then
					'ROMバンク0-3を吸い出したので、4-7バンクを吸い出す
					DBM(ex"\r\n")
					sdd1Bank+=4
					SDD1_SetBanks(hCOM,sdd1Bank)
					SDD1_PrintBanks(hCOM)
					DBM(ex"")

					DumpAddress=offset	'アドレスリセット
				Else
					'4-7バンクを吸い出し終わったので終了
					ExitDo
				End If
			End If
		End If
		if DumpAddress-offset>=Size Then ExitDo

		'進捗表示
		SendMessage(GetDlgItem(hMainWnd,hProg),PBM_STEPIT,0,0)
		SetWindowText(MWnd(Static7),Str$(Int(((DumpAddress-offset)*(sdd1Bank/4+1))/Size*100))+"%")
		DBMN(".")
	Loop
	time=GetTickCount()-lt

	DBM(ex"\r\nTime  : "+Str$(time)+ex"ms")
	DBM("Speed : "+Str$(Int(Size/(time/1000)) AS Long)+ex"byte/sec\r\n")

	if info->CheckSum2=(CheckSum And &HFFFF) and info->CheckSum1=(not(CheckSum) And &HFFFF) Then
		DBM(ex"Checksum OK")
		DBM(ex"Successful!!!")
	Else
		DBM(ex"Checksum Error!!!")
		MessageBoxf(hMainWnd,"ROM Dump Error",MB_ICONERROR, _
			ex"Checksum Error!\nFailed to dump ROM.\nChecksum1 : %04X -> %04X\nChecksum2 : %04X -> %04X", info->CheckSum1,(not(CheckSum) And &HFFFF),info->CheckSum2,(CheckSum And &HFFFF))
	Endif
	

*EXIT_DUMP
	SendMessage( MWnd(hProg), PBM_SETPOS, 0, 0 )
	SetWindowText(MWnd(Static7),"100%")
	SetDlgEnable(STATUS_CONNECTED)

	CloseHandle(hout)
	free(buf)

	SetAddress(hCOM,0,FALSE)
EndSub


'チェックサム計算
Function checkContiguous(adr AS BytePtr,size AS DWord) AS Long
	Dim i AS Long,someCount AS DWord,last AS Byte
	For i=0 To size
		if last=adr[i] Then
			/*if last<>0 Then*/ someCount++	'0連続はよくあるので除外
			
			if someCount=>&H1000 Then DBM(Hex$(last)+"fill"):ExitFunction
		Else
			someCount=0
		End If
		last=adr[i]
	Next i
	checkContiguous=TRUE
End Function

Function CalcChkSum(data AS BytePtr,length AS DWord) AS DWord
	Dim i AS DWord
	CalcChkSum=0
	Do
		CalcChkSum += data[i]
		if i => length-1 Then ExitDo
		i++
	Loop
End Function


/*  http://hongkongarduino.web.fc2.com/archive/forprogram.htm
    Ch4に3を設定。SRAMのOEとディスエーブル、CEをイネーブルにする。= CE:0 RST:1 OE:1
    MADチップの場合は7を出力する。								  = CE:1 RST:1 OE:1
    アドレスを設定する。
    74138のCBAを100にし、データ出力モードにする。
    このとき、STROBEはL(反転出力に注意)にしておく。
    STROBEをHにし、WEをイネーブルにする。
    BUSY線から74645が有効になっているか確認してからデータを出力する。
    STROBEをLにし、WEをディスエーブルにする。
    Ch4を1に設定する。MADチップの場合は5。 
*/
Sub WriteSRAM(hFile AS HANDLE,info AS *SFC_ROM_HEADER,FileName AS BytePtr)
	Dim hIn AS HANDLE,buf AS BytePtr
	Dim CtrMask as Byte

	if info->SRAMSize=0 Then DBM("SRAM Size is zero."):ExitSub
	DBM("SRAM Writeing...")
	DBM("Address:0x"+Hex$(info->SRAMAddress))
	PumpMessage()

	hIn=OpenFile(FileName,GENERIC_READ)	
	if info->SRAMSize <> GetFileSize(hIn,NULL) Then
		if MessageBox(hMainWnd,"ファイルサイズが不正です。続行しますか？",ProgramName+" - Write SRAM",MB_YESNO) = IDNO Then ExitSub
	Endif

	buf=calloc(info->SRAMSize)
	LoadFile(hIn,buf,info->SRAMSize)
	CloseHandle(hIn)

	if info->ROMType <> SFC_ROM_SA1 Then
		'ノーマルカートリッジ
		if info->Mad=TRUE Then
			CtrMask=SRAM_WRITE_MAD
		Else
			CtrMask=SRAM_WRITE_NORMAL
		Endif	

		SendControl(hFile,CtrMask)	' SRAM CE
		WriteROM(hFile,buf, 0, info->SRAMAddress , info->SRAMSize,FALSE)
	
	Else
		'SA-1 (sanni氏のcartreaderを参考)
		DBM("SA-1 Write BW-RAM (動作不能)")
		Dim cmd[512] AS Byte
		Dim block AS Long,lastBlock AS Long,curByte AS DWord,i AS Long

		lastBlock=info->SRAMSize/&H2000
		DBM("blocks = "+Hex$(lastBlock))

		SetCPU_Clock(hFile,TRUE)
	
		SendControl(hFile,RST or OE or CE or WE)'SRAM_WRITE_SA1)	' RST WE RD CE -> High

		for block=0 To lastBlock-1
			cmd[0]=block
			cmd[1]=block
			WriteROM(hFile,cmd,0,&H2224,2,FALSE)	'MAP SRAM to 0x6000~0x7FFF
			cmd[0]=&H80
			cmd[1]=&H80
			WriteROM(hFile,cmd,0,&H2226,2,FALSE)	'SRAM WRITE ENABLE
			DBM("Writeing block"+Str$(block))
			FillMemory(cmd,512,block+lastBlock)

			for curByte=0 To &H7FFF Step 512
				if curByte < &H6000 Then
				'	WriteROM(hFile,cmd,0,curByte,512,FALSE)
				Else
					WriteROM(hFile,buf+curByte-&H6000+block*&H2000,0,curByte,512,FALSE)
					DBM(Hex$(curByte-&H6000+block*&H2000)+" / "+Hex$(curByte))
				End If
				Sleep(10)

			Next curByte

			Sleep(1000)
			Exitfor
		Next block
		cmd[0]=0
		WriteROM(hFile,cmd,0,&H2227,1,FALSE)	'SRAM DISABLE

		SetCPU_Clock(hFile,FALSE)

		SendControl(hFile,12)	'RST LOW
	Endif

	DBM(ex"Done\r\n")
	free(buf)
	
	SendControl(hFile,DEFAULT_CTRLBUS)	'default
	SetAddress(hFile,0,FALSE)
EndSub

Sub DumpSRAM(hFile AS HANDLE,info AS *SFC_ROM_HEADER,FileName AS BytePtr)
	Dim hout AS HANDLE,buf AS BytePtr
	Dim CtrMask as Byte

	if info->SRAMSize=0 Then DBM("SRAM Size is zero."):ExitSub
	DBM("SRAM Dumping...")
	DBM("Address:0x"+Hex$(info->SRAMAddress))
	PumpMessage()
	
	buf=calloc(info->SRAMSize)

	if info->Mad=TRUE Then
		CtrMask=SRAM_READ_MAD
	Else
		CtrMask=SRAM_READ_NOMAL
	Endif	

	SendControl(hFile,CtrMask)	' SRAM CE
	ReadROM(hFile,buf, 0, info->SRAMAddress , info->SRAMSize,FALSE)

	hout=OpenFile(FileName,GENERIC_WRITE)
	OutFile(hout,buf,info->SRAMSize)
	CloseHandle(hout)

	DBM(ex"Done\r\n")
	free(buf)
	
	SetAddress(hFile,0,FALSE)
	SendControl(hFile,DEFAULT_CTRLBUS)	'default
EndSub

'生のROMヘッダをSFC_ROM_HEADERに
Sub SloveCartInfo(data AS BytePtr,info As *SFC_ROM_HEADER)
	'カートリッジのヘッダエリアのデータをSFC_ROM_HEADERへ格納

	Dim i as Long

	memcpy(info,data,21)
	For i=0 To 20
		if info->Title[i] = 0 Then
			lstrcpy(info->Title,"CART CONNECTION ERR")
			exitfor
		Endif
	Next i
	info->Title[21]=0

	info->CartType = data[22]
	info->MapMode=data[21]
	info->ROMType=(info->MapMode And &H0F)

	Select Case info->ROMType
		Case SFC_ROM_HiROM
			info->SRAMAddress=SRAM_ADDRESS_HiROM_NORMAL

		Case SRAM_ADDRESS_SA1
			info->SRAMAddress=SRAM_ADDRESS_SA1

		Case Else
			info->SRAMAddress=SRAM_ADDRESS_LoROM_NORMAL

	End Select
	
	info->ROMSize=&H0400<<data[23]

	if data[24]<>0 then
		info->SRAMSize = &H0400<<data[24]
	Else
		info->SRAMSize=0
	End If

	info->Country = data[25]
	info->Version = data[27]
	info->CheckSum1 = ((data[29] << 8) Or data[28]) AS Word
	info->CheckSum2 = ((data[31] << 8) Or data[30]) AS Word
EndSub



'SDD1のバンクをセットアップ
'SNESバンクC0, D0, E0, F0にbankStartから始まるROMバンク(1MByteごと)を割り当て
Sub SDD1_SetBanks(hCOM AS HANDLE,bankStart AS Long)
	Dim buf[3] AS Byte, i AS Long
	DBM(sprintfStr("Setting S-DD1 bank %d-%d...",bankStart,bankStart+3))

	For i=0 To 3
		buf[i] = bankStart+i
	Next i

	SendControl(hCOM,RST or OE)
	WriteROM(hCOM,buf,0,&H004804,4,FALSE)
	SendControl(hCOM,DEFAULT_CTRLBUS)
End Sub

'現在のSDD1のバンク設定をGUIに出力
Sub SDD1_PrintBanks(hCOM AS HANDLE)
	Dim buf[3] AS Byte, i AS Long

	SendControl(hCOM,DEFAULT_CTRLBUS)
	ReadROM(hCOM,buf,0,&H004804,4,FALSE)
			
	For i=0 To 3
		DBM(sprintfStr("%X00000h-%XFFFFFh : ROM bank%d",&HC+i,&HC+i,buf[i]))
	Next i

End Sub
